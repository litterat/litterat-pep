
buildscript {
  repositories {
    jcenter()
    maven { 
      url "https://repo.maven.apache.org/maven2"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "org.javamodularity:moduleplugin:1.5.0"
    classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'org.anarres.jarjar'

// Development environments
apply plugin: 'eclipse'
eclipse {
    classpath {
        defaultOutputDir = new File("$buildDir/eclipse");
        file {
            whenMerged {
                entries.findAll { isModule(it) }.each {
                    it.entryAttributes['module'] = 'true'
                }

                entries.findAll { isSource(it) && isTestScope(it) }.each {
                    it.entryAttributes['test'] = 'true'
                }

                entries.findAll { isLibrary(it) && isTestScope(it) }.each {
                    it.entryAttributes['test'] = 'true'
                }
            }
        }
    }

}

group = 'io.litterat'
version = '0.5.0'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-tree
	implementation group: 'org.ow2.asm', name: 'asm-tree', version: '8.0.1'
}

test {
	useJUnitPlatform()
}

modifyPom {
    project {
        name 'Pep'
        description 'Projection-embedded pairs library for use in serialization libraries'
        url 'http://litterat.io/'
        packaging 'jar'
        licenses {
            license {
                url 'https://github.com/litterat/pep-java/blob/master/LICENSE'
            }
        }
        scm {
            url 'https://github.com/litterat/pep-java'
            connection 'scm:git:https://github.com/litterat/pep-java.git'
            developerConnection 'scm:git@github.com:litterat/pep-java.git'
        }
        developers {
            developer {
                id 'oobles'
                name 'David Ryan'
            }
        }

    }
}

boolean isLibrary(entry) {
  return entry.properties.kind.equals('lib') || entry.properties.kind.equals('src')
}
boolean isTestScope(entry) {
  return entry.entryAttributes.get('gradle_used_by_scope').equals('test'); }
boolean isModule(entry) {
  return isLibrary(entry) && !isTestScope(entry); }
boolean isSource(entry) { return entry.properties.kind.equals('src'); }


